plugins {
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "java"
}

group = "me.dags"
version = "3.2-SNAPSHOT"
sourceCompatibility = 1.8
def spongeVersion = "7.2.0"
def spongeChannel = "SNAPSHOT"

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "http://repo.spongepowered.org/maven" }
}

dependencies {
    compileOnly "org.spongepowered:spongeapi:${spongeVersion}-${spongeChannel}"
    shade ("net.dv8tion:JDA:4.2.0_204") {
        // text-only
        exclude module: "opus-java"
        // provided by sponge
        exclude module: "slf4j-api"
    }
    shade ("com.github.dags-:CommandBus:2.0.1") {
        transitive = false
    }
    shade ("com.github.dags-:TextMU:0.2") {
        transitive = false
    }
}

shadowJar {
    configurations = [project.configurations.shade]

    // PLUGIN DEPENDENCIES //
    // sponge libs
    relocate "me.dags.commandbus.", "me.dags.discordsync.libs.commandbus."
    relocate "me.dags.textmu.", "me.dags.discordsync.libs.textmu."

    // JDA DEPENDENCIES //
    // http/json libs
    relocate "okio.", "me.dags.discordsync.libs.okio."
    relocate "okhttp3.", "me.dags.discordsync.libs.okhttp3."
    relocate "com.neovisionaries.", "me.dags.discordsync.libs.neovisionaries."
    relocate "com.fasterxml.", "me.dags.discordsync.libs.fasterxml."
    // jda jar packages
    relocate "com.iwebpp.", "me.dags.discordsync.libs.iwebpp."
    relocate "net.dv8tion.", "me.dags.discordsync.libs.dv8tion."
    // collection libs
    relocate "gnu.trove.", "me.dags.discordsync.libs.trove."
    relocate "org.apache.commons.collections4.", "me.dags.discordsync.libs.collections4."
    // utils
    relocate "javax.", "me.dags.discordsync.libs.findbugs."
    relocate "org.intellij.", "me.dags.discordsync.libs.intellij."
    relocate "org.jetbrains.", "me.dags.discordsync.libs.jetbrains."

    archiveName = "${baseName}-${spongeVersion}-${version}.jar"
}

build.dependsOn shadowJar
jar.enabled = false